diff --git a/src/examples/groovyShell/BlacklistingShell.groovy b/src/examples/groovyShell/BlacklistingShell.groovy
new file mode 100644
index 0000000000..d2961dc231
--- /dev/null
+++ b/src/examples/groovyShell/BlacklistingShell.groovy
@@ -0,0 +1,78 @@
+import org.codehaus.groovy.control.CompilerConfiguration
+import org.codehaus.groovy.control.MultipleCompilationErrorsException
+import org.codehaus.groovy.control.customizers.SecureASTCustomizer
+import org.codehaus.groovy.control.messages.ExceptionMessage
+import org.codehaus.groovy.ast.expr.MethodPointerExpression
+
+
+/**
+ * The blacklisting shell is similar to a GroovyShell in that it can evaluate text as
+ * code and return a result. It is intended as an example of using blacklisting to prevent
+ * running methods on a class - in this case, java.lang.System.  Please note that in creating
+ * any secure environment, there is no substitution for using a SecurityManager.
+ *
+ * Amoung the many different calls this class prevents are:
+ *   System.exit(0)
+ *   Eval.me("System.exit(0)")
+ *   evaluate("System.exit(0)")
+ *   (new GroovyShell()).evaluate("System.exit(0)")
+ *   Class.forName("java.lang.System").exit(0)
+ *   System.&exit.call(0)
+ *   System.getMetaClass().invokeMethod("exit",0)
+ *   def s = System; s.exit(0)
+ *   Script t = this; t.evaluate("System.exit(0)")
+ *
+ * The restrictions required, however, also prevent the following code from working:
+ *   println "test"
+ *   def s = "test" ; s.count("t") 
+ *
+ * @author Jim Driscoll (jamesgdriscoll@gmail.com)
+ */
+class BlacklistingShell {
+    
+    /**
+     * Compiles the text into a Groovy object and then executes it, returning the result.
+     * Prevents calling any method on java.lang.System within the VM
+     * @param text
+     *       the script to evaluate typed as a string
+     * @throws SecurityException
+     *       most likely the script is doing something other than arithmetic
+     * @throws IllegalStateException
+     *       if the script returns something other than a number
+     */
+    def evaluate(String text) {
+        try {
+            final SecureASTCustomizer secure = new SecureASTCustomizer()
+            secure.with {
+
+                receiversClassesBlackList = [
+                    Object,
+                    Script,
+                    GroovyShell,
+                    Eval,
+                    System,
+                ].asImmutable()
+                
+                expressionsBlacklist = [MethodPointerExpression].asImmutable()
+                
+            }
+            CompilerConfiguration config = new CompilerConfiguration()
+            config.addCompilationCustomizers(secure)
+            GroovyClassLoader loader = new GroovyClassLoader(this.class.classLoader, config)
+            Class clazz = loader.parseClass(text)
+            Script script = (Script) clazz.newInstance();
+            Object result = script.run()
+            return result
+        } catch (SecurityException ex) {
+            throw new SecurityException("Could not evaluate script: $text", ex)
+        } catch (MultipleCompilationErrorsException mce) {
+            //this allows compilation errors to be seen by the user       
+            mce.errorCollector.errors.each {
+                if (it instanceof ExceptionMessage && it.cause instanceof SecurityException) {
+                    throw it.cause
+                }
+            }
+            throw mce
+        }
+    }            
+}
\ No newline at end of file
diff --git a/src/examples/groovyShell/BlacklistingShellTest.groovy b/src/examples/groovyShell/BlacklistingShellTest.groovy
new file mode 100644
index 0000000000..0a0b59fc50
--- /dev/null
+++ b/src/examples/groovyShell/BlacklistingShellTest.groovy
@@ -0,0 +1,64 @@
+/**
+* Unit test for BlacklistingShell.
+* Requires JUnit to be in path, just like any other GroovyTestCase. 
+*
+* @author Jim Driscoll jamesgdriscoll@gmail.com
+*/
+class BlacklistingShellTest extends GroovyTestCase {
+
+    private BlacklistingShell shell
+
+    void setUp() {
+        shell = new BlacklistingShell()
+    }
+
+    Object evaluate(String text) {
+        shell.evaluate(text)
+    }
+
+    void testEvaluate_SuccessfulPaths() {
+        assert 6 == evaluate("++(5)")
+        assert 0 == evaluate("5 < 4 ? 1 : 0")
+        assert 0 == evaluate("5 != 4 ? 0 : 1")
+        assert 0 == evaluate("5 < 4 ?: 0 ")
+    }
+
+    void testEvaluate_Failures() {
+        shouldFail(SecurityException) {
+            evaluate('def c = System.class; c.forName("java.lang.System").exit(0)')
+        }
+        shouldFail(SecurityException) {
+            evaluate('Class.forName("java.lang.System").exit(0)')
+        }
+        shouldFail(SecurityException) {
+            evaluate('System.exit(0)')
+        }
+        shouldFail(SecurityException) {
+            evaluate('def e = System.&exit; e.call(0)')
+        }
+        shouldFail(SecurityException) {
+            evaluate('System.&exit.call(0)')
+        }
+        shouldFail(SecurityException) {
+            evaluate('System.getMetaClass().invokeMethod("exit",0)')
+        }
+        shouldFail(SecurityException) {
+            evaluate('evaluate("System.exit(0)")')
+        }
+        shouldFail(SecurityException) {
+            evaluate('(new GroovyShell()).evaluate("System.exit(0)")')
+        }
+        shouldFail(SecurityException) {
+            evaluate('def sh = new GroovyShell(); sh.evaluate("System.exit(0)")')
+        }
+        shouldFail(SecurityException) {
+            evaluate('Eval.me("System.exit(0)")')
+        }
+        shouldFail(SecurityException) {
+            evaluate('def s = System; s.exit(0)')
+        }
+        shouldFail(SecurityException) {
+            evaluate('Script t = this; t.evaluate("System.exit(0)")')
+        }
+    }
+}
\ No newline at end of file
diff --git a/src/main/org/codehaus/groovy/control/customizers/SecureASTCustomizer.java b/src/main/org/codehaus/groovy/control/customizers/SecureASTCustomizer.java
index 7ad20e6e9b..21483c1f75 100644
--- a/src/main/org/codehaus/groovy/control/customizers/SecureASTCustomizer.java
+++ b/src/main/org/codehaus/groovy/control/customizers/SecureASTCustomizer.java
@@ -456,6 +456,14 @@ public class SecureASTCustomizer extends CompilationCustomizer {
 
     /**
      * Sets the list of classes which deny method calls.
+     * 
+     * Please note that since Groovy is a dynamic language, and 
+     * this class performs a static type check, it will be reletively
+     * simple to bypass any blacklist unless the receivers blacklist contains, at
+     * a minimum, Object, Script, GroovyShell, and Eval. Additionally,
+     * it is necessary to also blacklist MethodPointerExpression in the
+     * expressions blacklist for the receivers blacklist to function
+     * as a security check.
      *
      * @param receiversBlackList the list of refused classes, as fully qualified names
      */
