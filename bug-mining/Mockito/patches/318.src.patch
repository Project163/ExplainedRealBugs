diff --git a/build.gradle b/build.gradle
deleted file mode 100644
index c759b5297..000000000
--- a/build.gradle
+++ /dev/null
@@ -1,34 +0,0 @@
-buildscript {
-    repositories {
-        mavenLocal() //for local testing of mockito-release-tools
-        google()
-        gradlePluginPortal()
-    }
-
-    dependencies {
-        classpath 'gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.16.1'
-
-        classpath "io.github.gradle-nexus:publish-plugin:2.0.0-rc-1"
-        classpath 'org.shipkit:shipkit-changelog:2.0.1'
-        classpath 'org.shipkit:shipkit-auto-version:2.1.0'
-
-        classpath 'com.google.googlejavaformat:google-java-format:1.24.0'
-        classpath 'com.android.tools.build:gradle:7.4.2'
-        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.24"
-    }
-}
-
-plugins {
-    id("eclipse")
-    id("com.diffplug.spotless") version "6.25.0"
-    id("com.github.ben-manes.versions") version "0.51.0"
-}
-
-apply from: "$rootDir/gradle/shipkit.gradle"
-apply from: "$rootDir/gradle/license.gradle"
-
-subprojects { proj ->
-    plugins.withId('java') {
-        proj.apply from: "$rootDir/gradle/spotless.gradle"
-    }
-}
diff --git a/build.gradle.kts b/build.gradle.kts
new file mode 100644
index 000000000..a720082d7
--- /dev/null
+++ b/build.gradle.kts
@@ -0,0 +1,38 @@
+buildscript {
+    repositories {
+        mavenLocal() //for local testing of mockito-release-tools
+        google()
+        gradlePluginPortal()
+    }
+
+    dependencies {
+        classpath("gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.16.1")
+
+        classpath("io.github.gradle-nexus:publish-plugin:2.0.0-rc-1")
+        classpath("org.shipkit:shipkit-changelog:2.0.1")
+        classpath("org.shipkit:shipkit-auto-version:2.1.0")
+
+        classpath("com.google.googlejavaformat:google-java-format:1.24.0")
+        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.24")
+    }
+}
+
+plugins {
+    id("eclipse")
+    id("com.diffplug.spotless") version "6.25.0"
+    id("com.github.ben-manes.versions") version "0.51.0"
+
+    // Top-level android plugin declaration required for :mockito-integration-tests:android-tests to work
+    alias(libs.plugins.android.application) apply false
+}
+
+apply {
+    from(rootProject.file("gradle/shipkit.gradle"))
+    from(rootProject.file("gradle/license.gradle"))
+}
+
+subprojects {
+    plugins.withId("java") {
+        apply(from = rootProject.file("gradle/spotless.gradle"))
+    }
+}
diff --git a/gradle/libs.versions.toml b/gradle/libs.versions.toml
index d6d6ebd70..ac848ca38 100644
--- a/gradle/libs.versions.toml
+++ b/gradle/libs.versions.toml
@@ -11,6 +11,8 @@ gradleplugin-animal-sniffer = "1.7.1"
 gradleplugin-aQute-bnd = "7.0.0"
 gradleplugin-errorprone = "4.1.0"
 
+gradleplugin-android = "7.4.2"
+
 [libraries]
 android-junit = { module = "androidx.test.ext:junit", version = "1.2.1" }
 android-runner = { module = "androidx.test:runner", version = "1.6.2" }
@@ -46,3 +48,6 @@ gradleplugin-errorprone = { module = "net.ltgt.gradle:gradle-errorprone-plugin",
 # animal sniffer compatibility signatures
 animalSniffer-android-apiLevel26 = { module = "net.sf.androidscents.signature:android-api-level-26", version = "8.0.0_r2" }
 animalSniffer-java = { module = "org.codehaus.mojo.signature:java18", version = "1.0" }
+
+[plugins]
+android-application = { id = "com.android.application", version.ref = "gradleplugin-android" }
diff --git a/mockito-integration-tests/android-tests/build.gradle b/mockito-integration-tests/android-tests/build.gradle
deleted file mode 100644
index 9776ad831..000000000
--- a/mockito-integration-tests/android-tests/build.gradle
+++ /dev/null
@@ -1,86 +0,0 @@
-// Top-level build file where you can add configuration options common to all sub-projects/modules.
-
-plugins {
-    id("com.android.application")
-    id("kotlin-android")
-    id("mockito.test-conventions")
-}
-
-android {
-    namespace = "org.mockitousage.androidtest"
-    compileSdk = 33
-
-    defaultConfig {
-        minSdk = 26
-        targetSdk = 33
-        versionCode = 1
-        versionName = "1.0"
-
-        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
-    }
-
-    buildTypes {
-        debug {
-            enableUnitTestCoverage true
-            enableAndroidTestCoverage true
-        }
-    }
-    afterEvaluate {
-        // Enable coverage of mockito classes which are not part of this module.
-        // Without these added classFileCollection dependencies the coverage data/report is empty.
-        tasks.createDebugAndroidTestCoverageReport { com.android.build.gradle.internal.coverage.JacocoReportTask task ->
-            // Clear the production code of this module: src/main/java, it has no mockito-user-visible code.
-            task.classFileCollection.setFrom()
-            rootProject.allprojects { Project currentProject ->
-                plugins.withId("java") {
-                    SourceSetContainer sourceSets = currentProject.sourceSets
-                    // Mimic org.gradle.testing.jacoco.tasks.JacocoReportBase.sourceSets().
-                    // Not possible to set task.javaSources because it's initialized with setDisallowChanges.
-                    //task.javaSources.add({ [ sourceSets.main.allJava.srcDirs ] })
-                    task.classFileCollection.from(
-                        sourceSets.main.output.asFileTree.matching {
-                            exclude("**/internal/util/concurrent/**")
-                        }
-                    )
-                }
-            }
-        }
-    }
-
-    compileOptions {
-        sourceCompatibility JavaVersion.VERSION_11
-        targetCompatibility JavaVersion.VERSION_11
-    }
-
-    kotlinOptions {
-        jvmTarget = '11'
-    }
-}
-
-androidComponents {
-    beforeVariants(selector().withBuildType("release")) {
-        enable = false
-    }
-}
-
-dependencies {
-    implementation libs.kotlin.stdlib
-
-    // Add :android on the classpath so that AGP's jacoco setup thinks it's "production code to be tested".
-    // Essentially a way to say: tasks.createDebugAndroidTestCoverageReport.classFileCollection.from(project(":android"))
-    runtimeOnly project(":mockito-extensions:mockito-android")
-
-    // Exclude mockito-android from JVM tests, because otherwise it clashes with :mockito-core.
-    configurations.testImplementation {
-        exclude group: 'org.mockito', module: 'mockito-android'
-    }
-
-    testImplementation project(":mockito-core")
-    testImplementation libs.junit4
-    testImplementation libs.junit.jupiter.api
-    testImplementation libs.junit.jupiter.engine
-
-    androidTestImplementation libs.android.runner
-    androidTestImplementation libs.android.junit
-    androidTestImplementation project(":mockito-extensions:mockito-android")
-}
diff --git a/mockito-integration-tests/android-tests/build.gradle.kts b/mockito-integration-tests/android-tests/build.gradle.kts
new file mode 100644
index 000000000..565fb8816
--- /dev/null
+++ b/mockito-integration-tests/android-tests/build.gradle.kts
@@ -0,0 +1,89 @@
+import com.android.build.gradle.internal.coverage.JacocoReportTask
+
+// Module-level android configuration.
+// https://developer.android.com/build/migrate-to-catalogs
+
+plugins {
+    alias(libs.plugins.android.application)
+    id("kotlin-android")
+    id("mockito.test-conventions")
+}
+
+android {
+    namespace = "org.mockitousage.androidtest"
+    compileSdk = 33
+
+    defaultConfig {
+        minSdk = 26
+        targetSdk = 33
+        versionCode = 1
+        versionName = "1.0"
+
+        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
+    }
+
+    buildTypes {
+        debug {
+            enableUnitTestCoverage = true
+            enableAndroidTestCoverage = true
+        }
+    }
+    afterEvaluate {
+        // Enable coverage of mockito classes which are not part of this module.
+        // Without these added classFileCollection dependencies the coverage data/report is empty.
+        tasks.named<JacocoReportTask>("createDebugAndroidTestCoverageReport") jacocoTask@{
+            // Clear the production code of this module: src/main/java, it has no mockito-user-visible code.
+            classFileCollection.setFrom()
+            rootProject.allprojects currentProject@{
+                plugins.withId("java") {
+                    val sourceSets = this@currentProject.sourceSets
+                    // Mimic org.gradle.testing.jacoco.tasks.JacocoReportBase.sourceSets().
+                    // Not possible to set task.javaSources because it's initialized with setDisallowChanges.
+                    //task.javaSources.add({ [ sourceSets.main.allJava.srcDirs ] })
+                    this@jacocoTask.classFileCollection.from(
+                        sourceSets.named("main").get().output.asFileTree.matching {
+                            exclude("**/internal/util/concurrent/**")
+                        }
+                    )
+                }
+            }
+        }
+    }
+
+    compileOptions {
+        sourceCompatibility = JavaVersion.VERSION_11
+        targetCompatibility = JavaVersion.VERSION_11
+    }
+
+    kotlinOptions {
+        jvmTarget = "11"
+    }
+}
+
+androidComponents {
+    beforeVariants(selector().withBuildType("release")) {
+        it.enable = false
+    }
+}
+
+// Exclude mockito-android from JVM tests, because otherwise it clashes with :mockito-core.
+configurations.testImplementation {
+    exclude (group = "org.mockito", module = "mockito-android")
+}
+
+dependencies {
+    implementation(libs.kotlin.stdlib)
+
+    // Add :android on the classpath so that AGP's jacoco setup thinks it's "production code to be tested".
+    // Essentially a way to say: tasks.createDebugAndroidTestCoverageReport.classFileCollection.from(project(":android"))
+    runtimeOnly(project(":mockito-extensions:mockito-android"))
+
+    testImplementation(project(":mockito-core"))
+    testImplementation(libs.junit4)
+    testImplementation(libs.junit.jupiter.api)
+    testImplementation(libs.junit.jupiter.engine)
+
+    androidTestImplementation(libs.android.runner)
+    androidTestImplementation(libs.android.junit)
+    androidTestImplementation(project(":mockito-extensions:mockito-android"))
+}
diff --git a/settings.gradle.kts b/settings.gradle.kts
index 7443c3720..9d3988cdb 100644
--- a/settings.gradle.kts
+++ b/settings.gradle.kts
@@ -1,6 +1,14 @@
 pluginManagement {
     repositories {
         gradlePluginPortal()
+        google()
+    }
+}
+
+dependencyResolutionManagement {
+    repositories {
+        google()
+        mavenCentral()
     }
 }
 
