diff --git a/build.gradle b/build.gradle
index 0410aa94c..b0442b5c5 100644
--- a/build.gradle
+++ b/build.gradle
@@ -7,7 +7,6 @@ buildscript {
 
     dependencies {
         classpath 'gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.16.1'
-        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:4.0.1'
 
         classpath "io.github.gradle-nexus:publish-plugin:2.0.0-rc-1"
         classpath 'org.shipkit:shipkit-changelog:2.0.1'
@@ -29,36 +28,7 @@ apply from: "$rootDir/gradle/root/ide.gradle"
 apply from: "$rootDir/gradle/license.gradle"
 
 subprojects { proj ->
-    repositories {
-        mavenCentral()
-        google()
-    }
     plugins.withId('java') {
-        proj.apply from: "$rootDir/gradle/errorprone.gradle"
-        proj.apply from: "$rootDir/gradle/jfr.gradle"
         proj.apply from: "$rootDir/gradle/spotless.gradle"
     }
-    tasks.withType(JavaCompile).configureEach {
-        //I don't believe those warnings add value given modern IDEs
-        options.warnings = false
-        options.encoding = 'UTF-8'
-    }
-    tasks.withType(Javadoc).configureEach {
-        options.addStringOption('Xdoclint:none', '-quiet')
-        options.addStringOption('encoding', 'UTF-8')
-        options.addStringOption('charSet', 'UTF-8')
-        options.setSource('11')
-    }
-
-    tasks.withType(AbstractArchiveTask).configureEach {
-        preserveFileTimestamps = false
-        reproducibleFileOrder = true
-        dirMode = Integer.parseInt("0755", 8)
-        fileMode = Integer.parseInt("0644", 8)
-    }
-
-    apply plugin: 'checkstyle'
-    checkstyle {
-       configFile = rootProject.file('config/checkstyle/checkstyle.xml')
-    }
 }
diff --git a/buildSrc/build.gradle.kts b/buildSrc/build.gradle.kts
index f714d56e6..75d1c1273 100644
--- a/buildSrc/build.gradle.kts
+++ b/buildSrc/build.gradle.kts
@@ -7,6 +7,7 @@ dependencies {
     implementation(libs.gradleplugin.testLogger)
     implementation(libs.gradleplugin.animalSniffer)
     implementation(libs.gradleplugin.bnd)
+    implementation(libs.gradleplugin.errorprone)
 
     // https://github.com/gradle/gradle/issues/15383
     implementation(files(libs.javaClass.superclass.protectionDomain.codeSource.location))
diff --git a/buildSrc/src/main/kotlin/mockito.checkstyle-conventions.gradle.kts b/buildSrc/src/main/kotlin/mockito.checkstyle-conventions.gradle.kts
new file mode 100644
index 000000000..3fda6bd2c
--- /dev/null
+++ b/buildSrc/src/main/kotlin/mockito.checkstyle-conventions.gradle.kts
@@ -0,0 +1,19 @@
+import org.gradle.api.plugins.quality.Checkstyle
+import org.gradle.kotlin.dsl.checkstyle
+import org.gradle.kotlin.dsl.withType
+
+plugins {
+    checkstyle
+}
+
+checkstyle {
+    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
+}
+
+tasks.withType<Checkstyle>().configureEach {
+    reports {
+        xml.required.set(false)
+        html.required.set(true)
+        html.stylesheet = resources.text.fromFile("$rootDir/config/checkstyle/checkstyle.xsl")
+    }
+}
diff --git a/buildSrc/src/main/kotlin/mockito.java-conventions.gradle.kts b/buildSrc/src/main/kotlin/mockito.java-conventions.gradle.kts
index ec66850db..1e41ef22d 100644
--- a/buildSrc/src/main/kotlin/mockito.java-conventions.gradle.kts
+++ b/buildSrc/src/main/kotlin/mockito.java-conventions.gradle.kts
@@ -17,6 +17,18 @@ plugins.withType(JavaPlugin::class) {
             options.isWarnings = false
             options.encoding = "UTF-8"
         }
+
+        withType<Javadoc>().configureEach {
+            options {
+                encoding = "UTF-8"
+                source = javaReleaseVersion.toString()
+
+                if (this is StandardJavadocDocletOptions) {
+                    addStringOption("Xdoclint:none", "-quiet")
+                    addStringOption("charSet", "UTF-8")
+                }
+            }
+        }
     }
 }
 
diff --git a/buildSrc/src/main/kotlin/mockito.java-library-conventions.gradle.kts b/buildSrc/src/main/kotlin/mockito.java-library-conventions.gradle.kts
index 4d042e024..ec2b22124 100644
--- a/buildSrc/src/main/kotlin/mockito.java-library-conventions.gradle.kts
+++ b/buildSrc/src/main/kotlin/mockito.java-library-conventions.gradle.kts
@@ -4,6 +4,7 @@ plugins {
     id("mockito.test-conventions")
     id("mockito.test-retry-conventions")
     id("mockito.test-launcher-conventions")
+    id("net.ltgt.errorprone")
 }
 
 afterEvaluate {
@@ -14,10 +15,26 @@ afterEvaluate {
     }
 }
 
-tasks.withType<Checkstyle>().configureEach {
-    reports {
-        xml.required.set(false)
-        html.required.set(true)
-        html.stylesheet = resources.text.fromFile("$rootDir/config/checkstyle/checkstyle.xsl")
+repositories {
+    mavenCentral()
+    google()
+}
+
+dependencies {
+    errorprone(libs.errorprone)
+}
+
+tasks {
+    withType<AbstractArchiveTask>().configureEach {
+        isPreserveFileTimestamps = false
+        isReproducibleFileOrder = true
+        // dirMode = Integer.parseInt("0755", 8)
+        dirPermissions {
+            unix("rwxr-xr-x") // 0755
+        }
+        // fileMode = Integer.parseInt("0644", 8)
+        filePermissions {
+            unix("rw-r--r--") // 0644
+        }
     }
 }
diff --git a/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts b/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts
index 5a302a996..62581f467 100644
--- a/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts
+++ b/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts
@@ -3,4 +3,5 @@ plugins {
     id("mockito.java-library-conventions")
     id("mockito.osgi-conventions")
     id("mockito.publication-conventions")
+    id("mockito.checkstyle-conventions")
 }
diff --git a/buildSrc/src/main/kotlin/mockito.test-conventions.gradle.kts b/buildSrc/src/main/kotlin/mockito.test-conventions.gradle.kts
index 6bc5c18f8..d02eb02bc 100644
--- a/buildSrc/src/main/kotlin/mockito.test-conventions.gradle.kts
+++ b/buildSrc/src/main/kotlin/mockito.test-conventions.gradle.kts
@@ -5,6 +5,11 @@ plugins {
     id("com.adarshr.test-logger")
 }
 
+repositories {
+    mavenCentral()
+    google()
+}
+
 testlogger {
     theme = ThemeType.MOCHA_PARALLEL
     isShowPassed = false
diff --git a/buildSrc/src/main/kotlin/mockito.test-jfr-profiling-conventions.gradle.kts b/buildSrc/src/main/kotlin/mockito.test-jfr-profiling-conventions.gradle.kts
new file mode 100644
index 000000000..d3edda47a
--- /dev/null
+++ b/buildSrc/src/main/kotlin/mockito.test-jfr-profiling-conventions.gradle.kts
@@ -0,0 +1,34 @@
+plugins.withType(JavaPlugin::class) {
+    tasks.withType<Test>().configureEach {
+        val taskName = name
+        /*
+         * If the gradle option -Pjfr is set during test execution, JFR will be activated for that execution.
+         */
+        if (project.extensions.extraProperties.has("jfr")) {
+            val jfrDir = "build/jfr"
+            val jfrConfigFile = rootProject.file("config/jfr/jfr_config.jfc")
+            val jfrFile = project.file("$jfrDir/${taskName}_Exec.jfr")
+
+            jvmArgs(
+                //https://docs.oracle.com/en/java/javase/17/docs/specs/man/java.html
+                // Search for -XX:FlightRecorderOptions and -XX:StartFlightRecording for more details in the documentation
+                "-XX:+UnlockDiagnosticVMOptions",
+                "-XX:+DebugNonSafepoints",
+                "-XX:FlightRecorderOptions=stackdepth=1024",
+                "-XX:StartFlightRecording=name=TestExec_$taskName,disk=true,maxsize=1g,dumponexit=true,filename=$jfrFile,settings=${jfrConfigFile.absolutePath}")
+
+            //We do not want to be UP-TO-DATE, if we are doing a Java Flight Recording. We always want a new recording.
+            outputs.upToDateWhen { false }
+
+            doFirst {
+                //Ensure that the jfr folder exists, otherwise the Java process start will fail.
+                project.file(jfrDir).mkdirs()
+            }
+
+            doLast{
+                logger.lifecycle("Java Flight Recording was written to: $jfrFile")
+            }
+        }
+    }
+}
+
diff --git a/gradle/errorprone.gradle b/gradle/errorprone.gradle
deleted file mode 100644
index d3066e90c..000000000
--- a/gradle/errorprone.gradle
+++ /dev/null
@@ -1,6 +0,0 @@
-
-apply plugin: "net.ltgt.errorprone"
-
-dependencies {
-    errorprone libs.errorprone
-}
diff --git a/gradle/jfr.gradle b/gradle/jfr.gradle
deleted file mode 100644
index d57cefbb1..000000000
--- a/gradle/jfr.gradle
+++ /dev/null
@@ -1,31 +0,0 @@
-tasks.withType(Test).configureEach {
-    def taskName = name
-    /*
-     * If the gradle option -Pjfr is set during test execution, JFR will be activated for that execution.
-     */
-    if (project.extensions.extraProperties.has("jfr")) {
-        def jfrDir = "build/jfr"
-        def jfrConfigFile = rootProject.file("config/jfr/jfr_config.jfc")
-        def jfrFile = project.file("$jfrDir/${taskName}_Exec.jfr")
-
-        jvmArgs(
-            //https://docs.oracle.com/en/java/javase/17/docs/specs/man/java.html
-            // Search for -XX:FlightRecorderOptions and -XX:StartFlightRecording for more details in the documentation
-            "-XX:+UnlockDiagnosticVMOptions",
-            "-XX:+DebugNonSafepoints",
-            "-XX:FlightRecorderOptions=stackdepth=1024",
-            "-XX:StartFlightRecording=name=TestExec_$taskName,disk=true,maxsize=1g,dumponexit=true,filename=$jfrFile,settings=${jfrConfigFile.absolutePath}")
-
-        //We do not want to be UP-TO-DATE, if we are doing a Java Flight Recording. We always want a new recording.
-        outputs.upToDateWhen { false }
-
-        doFirst {
-            //Ensure that the jfr folder exists, otherwise the Java process start will fail.
-            project.file(jfrDir).mkdirs()
-        }
-
-        doLast{
-            println("Java Flight Recording was written to: $jfrFile")
-        }
-    }
-}
diff --git a/gradle/libs.versions.toml b/gradle/libs.versions.toml
index af3f754dc..1952098dd 100644
--- a/gradle/libs.versions.toml
+++ b/gradle/libs.versions.toml
@@ -5,9 +5,10 @@ junit-jupiter = "5.11.2"
 junit-platform = "1.11.2"
 kotlin = "2.0.21"
 
-test-logger = "4.0.0"
-animal-sniffer = "1.7.1"
-aQute-bnd = "7.0.0"
+gradleplugin-test-logger = "4.0.0"
+gradleplugin-animal-sniffer = "1.7.1"
+gradleplugin-aQute-bnd = "7.0.0"
+gradleplugin-errorprone = "4.0.1"
 
 [libraries]
 android-junit = { module = "androidx.test.ext:junit", version = "1.2.1" }
@@ -36,9 +37,10 @@ opentest4j = { module = "org.opentest4j:opentest4j", version = "1.3.0" }
 osgi = { module = "org.osgi:osgi.core", version = "8.0.0" }
 
 # For buildSrc logic
-gradleplugin-testLogger = { module = "com.adarshr:gradle-test-logger-plugin", version.ref = "test-logger" }
-gradleplugin-animalSniffer = { module = "ru.vyarus:gradle-animalsniffer-plugin", version.ref = "animal-sniffer" }
-gradleplugin-bnd = { module = "biz.aQute.bnd:biz.aQute.bnd.gradle", version.ref = "aQute-bnd" }
+gradleplugin-testLogger = { module = "com.adarshr:gradle-test-logger-plugin", version.ref = "gradleplugin-test-logger" }
+gradleplugin-animalSniffer = { module = "ru.vyarus:gradle-animalsniffer-plugin", version.ref = "gradleplugin-animal-sniffer" }
+gradleplugin-bnd = { module = "biz.aQute.bnd:biz.aQute.bnd.gradle", version.ref = "gradleplugin-aQute-bnd" }
+gradleplugin-errorprone = { module = "net.ltgt.gradle:gradle-errorprone-plugin", version.ref = "gradleplugin-errorprone" }
 
 # animal sniffer compatibility signatures
 animalSniffer-android-apiLevel26 = { module = "net.sf.androidscents.signature:android-api-level-26", version = "8.0.0_r2" }
