diff --git a/mockito-integration-tests/osgi-tests/build.gradle b/mockito-integration-tests/osgi-tests/build.gradle
deleted file mode 100644
index 64a5847be..000000000
--- a/mockito-integration-tests/osgi-tests/build.gradle
+++ /dev/null
@@ -1,58 +0,0 @@
-plugins {
-    id("java")
-    id("mockito.test-conventions")
-}
-
-apply from: "osgi-test-bundles.gradle"
-
-description = "Test suite for OSGi framework with Mockito"
-
-dependencies {
-    testImplementation project(":mockito-core")
-    testImplementation libs.junit4
-    testImplementation libs.osgi
-
-    testRuntimeOnly libs.equinox
-}
-
-configurations {
-    testRuntimeBundles
-}
-
-dependencies {
-    testRuntimeBundles project(":mockito-core")
-    testRuntimeBundles libs.bytebuddy
-    testRuntimeBundles libs.objenesis
-    testRuntimeBundles tasks.testBundle.outputs.files
-    testRuntimeBundles tasks.otherBundle.outputs.files
-}
-
-test {
-    jvmArgumentProviders.add(
-        new RuntimeBundlesProvider(files: configurations.testRuntimeBundles.asFileTree)
-    )
-    dependsOn configurations.testRuntimeBundles
-    inputs.files(sourceSets.testBundle.allSource)
-        .withPathSensitivity(PathSensitivity.RELATIVE)
-        .withPropertyName('testBundleSources')
-    inputs.files(sourceSets.otherBundle.allSource)
-        .withPathSensitivity(PathSensitivity.RELATIVE)
-        .withPropertyName('otherBundleSources')
-    useJUnit()
-}
-
-/**
- * A helper class to pass classpath elements as relative paths. This allows the build
- * to be checked out in different locations on the file system and still hit the cache.
- */
-class RuntimeBundlesProvider implements CommandLineArgumentProvider {
-    @InputFiles
-    @PathSensitive(PathSensitivity.RELATIVE)
-    FileTree files
-
-    @Override
-    Iterable<String> asArguments() {
-        String[] absolutePaths = files.stream().map {it.absolutePath}.toArray()
-        ["-DtestRuntimeBundles=${absolutePaths.join(File.pathSeparator)}".toString()]
-    }
-}
diff --git a/mockito-integration-tests/osgi-tests/build.gradle.kts b/mockito-integration-tests/osgi-tests/build.gradle.kts
new file mode 100644
index 000000000..d53aa47a1
--- /dev/null
+++ b/mockito-integration-tests/osgi-tests/build.gradle.kts
@@ -0,0 +1,90 @@
+import aQute.bnd.gradle.Bundle
+
+plugins {
+    id("java")
+    id("mockito.test-conventions")
+    id("mockito.osgi-conventions")
+}
+
+description = "Test suite for OSGi framework with Mockito"
+
+dependencies {
+    testImplementation(project(":mockito-core"))
+    testImplementation(libs.junit4)
+    testImplementation(libs.osgi)
+
+    testRuntimeOnly(libs.equinox)
+}
+
+val testRuntimeBundles: Configuration by configurations.creating
+
+val (otherBundle, otherBundleTask) = configureOsgiBundle(project, "otherBundle")
+val (testBundle, testBundleTask) = configureOsgiBundle(project, "testBundle") {
+    add("testBundleImplementation", project(":mockito-core"))
+    add("testBundleImplementation", project.libs.junit4)
+    add("testBundleImplementation", otherBundle.output)
+}
+
+dependencies {
+    testRuntimeBundles(project(":mockito-core"))
+    testRuntimeBundles(libs.bytebuddy)
+    testRuntimeBundles(libs.objenesis)
+
+    testRuntimeBundles(testBundleTask.map { it.outputs.files })
+    testRuntimeBundles(otherBundleTask.map { it.outputs.files })
+}
+
+tasks {
+    test {
+        jvmArgumentProviders.add(RuntimeBundlesProvider(testRuntimeBundles.asFileTree))
+        dependsOn(testRuntimeBundles)
+
+        inputs.files(testBundle.allSource)
+            .withPathSensitivity(PathSensitivity.RELATIVE)
+            .withPropertyName("testBundleSources")
+
+        inputs.files(otherBundle.allSource)
+            .withPathSensitivity(PathSensitivity.RELATIVE)
+            .withPropertyName("otherBundleSources")
+        useJUnit()
+    }
+}
+
+/**
+ * A helper class to pass classpath elements as relative paths. This allows the build
+ * to be checked out in different locations on the file system and still hit the cache.
+ */
+class RuntimeBundlesProvider(
+    @InputFiles
+    @PathSensitive(PathSensitivity.RELATIVE)
+    val files: FileTree
+) : CommandLineArgumentProvider {
+    override fun asArguments(): Iterable<String> {
+        val absolutePaths = files.files.map { it.absolutePath }
+        return listOf("-DtestRuntimeBundles=${absolutePaths.joinToString(separator = File.pathSeparator)}")
+    }
+}
+
+fun configureOsgiBundle(project: Project, bundleName: String, dependencyConfig: DependencyHandler.() -> Unit = {}): Pair<SourceSet, TaskProvider<Bundle>> {
+    val bundle = project.sourceSets.create(bundleName)
+
+    project.dependencies {
+        dependencyConfig()
+    }
+
+    val bundleTask = project.tasks.register(bundleName, Bundle::class) {
+        archiveAppendix = name
+        bundle { // this: BundleTaskExtension
+            bnd(
+                """
+                    Bundle-SymbolicName: $name
+                    -exportcontents: *
+                    """.trimIndent()
+            )
+        }
+        from(bundle.output)
+    }
+
+    return bundle to bundleTask
+}
+
diff --git a/mockito-integration-tests/osgi-tests/osgi-test-bundles.gradle b/mockito-integration-tests/osgi-tests/osgi-test-bundles.gradle
deleted file mode 100644
index 2e14f5ca2..000000000
--- a/mockito-integration-tests/osgi-tests/osgi-test-bundles.gradle
+++ /dev/null
@@ -1,41 +0,0 @@
-buildscript {
-    repositories {
-        mavenCentral()
-    }
-    dependencies {
-        classpath libs.bnd.gradle
-    }
-}
-
-// Test bundles for OSGi tests
-
-sourceSets {
-    testBundle
-    otherBundle
-}
-
-dependencies {
-    testBundleImplementation project(":mockito-core")
-    testBundleImplementation libs.junit4
-    testBundleImplementation sourceSets.otherBundle.output
-}
-
-import aQute.bnd.gradle.Bundle
-
-task testBundle(type: Bundle) {
-    from sourceSets.testBundle.output
-}
-
-tasks.register('otherBundle', Bundle) {
-    from sourceSets.otherBundle.output
-}
-
-tasks.withType(Bundle).configureEach {
-    archiveAppendix = name
-    bundle { // this: BundleTaskExtension
-        bnd = """
-            Bundle-SymbolicName: $name
-            -exportcontents: *
-        """.stripIndent()
-    }
-}
