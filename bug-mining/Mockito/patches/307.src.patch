diff --git a/buildSrc/src/main/kotlin/mockito.java-library-conventions.gradle.kts b/buildSrc/src/main/kotlin/mockito.java-library-conventions.gradle.kts
new file mode 100644
index 000000000..d8d1c777f
--- /dev/null
+++ b/buildSrc/src/main/kotlin/mockito.java-library-conventions.gradle.kts
@@ -0,0 +1,25 @@
+plugins {
+    `java-library`
+    id("mockito.test-conventions")
+}
+
+afterEvaluate {
+    if (!base.archivesName.get().startsWith("mockito-")) {
+        // TODO throw GradleException("Project module should be prefixed with `mockito-` : ${project.projectDir})")
+        logger.warn("Project module should be prefixed with `mockito-` : ${project.projectDir})")
+        base.archivesName = "mockito-" + project.name
+    }
+}
+
+java {
+    sourceCompatibility = JavaVersion.VERSION_11
+    targetCompatibility = JavaVersion.VERSION_11
+}
+
+tasks.withType<Checkstyle>().configureEach {
+    reports {
+        xml.required.set(false)
+        html.required.set(true)
+        html.stylesheet = resources.text.fromFile("$rootDir/config/checkstyle/checkstyle.xsl")
+    }
+}
diff --git a/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts b/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts
index cba5fd25d..3758e3866 100644
--- a/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts
+++ b/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts
@@ -1,4 +1,5 @@
 plugins {
     base
+    id("mockito.java-library-conventions")
     id("mockito.osgi-conventions")
 }
diff --git a/buildSrc/src/main/kotlin/mockito.test-conventions.gradle.kts b/buildSrc/src/main/kotlin/mockito.test-conventions.gradle.kts
new file mode 100644
index 000000000..a1cbf657e
--- /dev/null
+++ b/buildSrc/src/main/kotlin/mockito.test-conventions.gradle.kts
@@ -0,0 +1,36 @@
+import org.gradle.api.tasks.testing.logging.TestExceptionFormat
+
+plugins {
+    java
+}
+
+tasks {
+    named<Test>("test") {
+        include("**/*Test.class")
+
+        testLogging {
+            exceptionFormat = TestExceptionFormat.FULL
+            showCauses = true
+        }
+
+        if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)
+            && System.getenv("MEMBER_ACCESSOR") == "member-accessor-reflection") {
+            jvmArgs("--add-opens=java.base/java.lang=ALL-UNNAMED")
+        }
+    }
+
+
+    withType<Test> {
+        javaLauncher = javaToolchains.launcherFor {
+            languageVersion = providers
+                .gradleProperty("mockito.test.java")
+                .map {
+                    if (it == "auto") {
+                        JavaLanguageVersion.of(JavaVersion.current().majorVersion)
+                    } else {
+                        JavaLanguageVersion.of(it)
+                    }
+                }
+        }
+    }
+}
diff --git a/gradle/java-library.gradle b/gradle/java-library.gradle
index a0588b27a..987f8f08e 100644
--- a/gradle/java-library.gradle
+++ b/gradle/java-library.gradle
@@ -1,11 +1,5 @@
 apply plugin: "java"
 apply plugin: "java-library"
-apply from: "$rootDir/gradle/test-launcher.gradle"
-
-if (!base.archivesName.get().startsWith("mockito-")) {
-    logger.warn("Project module should be prefixed with `mockito-` : ${project.projectDir})")
-    base.archivesName = "mockito-" + project.name
-}
 
 apply from: "$rootDir/gradle/java-publication.gradle"
 
@@ -16,31 +10,5 @@ generatePomFileForJavaLibraryPublication.doLast {
     assert pom.name == base.archivesName.get()
 }
 
-java {
-    sourceCompatibility = 11
-    targetCompatibility = 11
-}
-
-tasks.named("test", Test) {
-    include "**/*Test.class"
-
-    testLogging {
-        exceptionFormat 'full'
-        showCauses true
-    }
-
-    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)
-        && 'member-accessor-reflection' == System.getenv('MEMBER_ACCESSOR')) {
-        jvmArgs = ['--add-opens=java.base/java.lang=ALL-UNNAMED']
-    }
-}
-
 apply from: "$rootDir/gradle/retry-test.gradle"
 
-tasks.withType(Checkstyle).configureEach {
-    reports {
-        xml.required = false
-        html.required = true
-        html.stylesheet resources.text.fromFile("$rootDir/config/checkstyle/checkstyle.xsl")
-    }
-}
diff --git a/mockito-extensions/mockito-android/build.gradle b/mockito-extensions/mockito-android/build.gradle
index 377c09143..e39e69ae4 100644
--- a/mockito-extensions/mockito-android/build.gradle
+++ b/mockito-extensions/mockito-android/build.gradle
@@ -1,3 +1,7 @@
+plugins {
+    id("mockito.java-library-conventions")
+}
+
 apply from: "$rootDir/gradle/java-library.gradle"
 
 description = "Mockito for Android"
diff --git a/mockito-extensions/mockito-errorprone/build.gradle b/mockito-extensions/mockito-errorprone/build.gradle
index 988c4e5cc..282075fa2 100644
--- a/mockito-extensions/mockito-errorprone/build.gradle
+++ b/mockito-extensions/mockito-errorprone/build.gradle
@@ -1,3 +1,7 @@
+plugins {
+    id("mockito.java-library-conventions")
+}
+
 apply from: "$rootDir/gradle/java-library.gradle"
 
 description = "ErrorProne plugins for Mockito"
diff --git a/mockito-extensions/mockito-junit-jupiter/build.gradle b/mockito-extensions/mockito-junit-jupiter/build.gradle
index 1080f36cf..05508321d 100644
--- a/mockito-extensions/mockito-junit-jupiter/build.gradle
+++ b/mockito-extensions/mockito-junit-jupiter/build.gradle
@@ -1,7 +1,7 @@
 import aQute.bnd.gradle.Resolve
 
 plugins {
-    id 'biz.aQute.bnd.builder'
+    id("mockito.library-conventions")
 }
 
 apply from: "$rootDir/gradle/java-library.gradle"
diff --git a/mockito-extensions/mockito-proxy/build.gradle b/mockito-extensions/mockito-proxy/build.gradle
index 29c74b4c5..3ef9d2b54 100644
--- a/mockito-extensions/mockito-proxy/build.gradle
+++ b/mockito-extensions/mockito-proxy/build.gradle
@@ -1,3 +1,7 @@
+plugins {
+    id("mockito.java-library-conventions")
+}
+
 apply from: "$rootDir/gradle/java-library.gradle"
 
 description = "Mockito preconfigured proxy mock mock maker (to support interfaces without code generation)"
diff --git a/mockito-extensions/mockito-subclass/build.gradle b/mockito-extensions/mockito-subclass/build.gradle
index 1f91ad678..b8a5e6f43 100644
--- a/mockito-extensions/mockito-subclass/build.gradle
+++ b/mockito-extensions/mockito-subclass/build.gradle
@@ -1,3 +1,6 @@
+plugins {
+    id("mockito.java-library-conventions")
+}
 apply from: "$rootDir/gradle/java-library.gradle"
 
 description = "Mockito preconfigured subclass mock maker"
