diff --git a/buildSrc/build.gradle.kts b/buildSrc/build.gradle.kts
new file mode 100644
index 000000000..2969176ca
--- /dev/null
+++ b/buildSrc/build.gradle.kts
@@ -0,0 +1,11 @@
+plugins {
+    // doc: https://docs.gradle.org/current/userguide/kotlin_dsl.html
+    `kotlin-dsl`
+}
+
+dependencies {
+    implementation(libs.gradleplugin.animalSniffer)
+
+    // https://github.com/gradle/gradle/issues/15383
+    implementation(files(libs.javaClass.superclass.protectionDomain.codeSource.location))
+}
diff --git a/buildSrc/settings.gradle.kts b/buildSrc/settings.gradle.kts
new file mode 100644
index 000000000..1a4794687
--- /dev/null
+++ b/buildSrc/settings.gradle.kts
@@ -0,0 +1,11 @@
+dependencyResolutionManagement {
+    repositories {
+        mavenCentral()
+        gradlePluginPortal()
+    }
+    versionCatalogs {
+        create("libs") {
+            from(files("../gradle/libs.versions.toml"))
+        }
+    }
+}
diff --git a/buildSrc/src/main/kotlin/Common.kt b/buildSrc/src/main/kotlin/Common.kt
new file mode 100644
index 000000000..9c5663e07
--- /dev/null
+++ b/buildSrc/src/main/kotlin/Common.kt
@@ -0,0 +1,5 @@
+import org.gradle.api.Project
+import org.gradle.kotlin.dsl.the
+
+val Project.libs
+    get() = the<org.gradle.accessors.dm.LibrariesForLibs>()
diff --git a/buildSrc/src/main/kotlin/mockito.java-backward-compatibility-checks-conventions.gradle.kts b/buildSrc/src/main/kotlin/mockito.java-backward-compatibility-checks-conventions.gradle.kts
new file mode 100644
index 000000000..d19efc664
--- /dev/null
+++ b/buildSrc/src/main/kotlin/mockito.java-backward-compatibility-checks-conventions.gradle.kts
@@ -0,0 +1,30 @@
+import ru.vyarus.gradle.plugin.animalsniffer.AnimalSnifferExtension
+
+plugins {
+    java // for sourceSets
+    id("ru.vyarus.animalsniffer")
+}
+
+
+val main: SourceSet by sourceSets.getting
+configure<AnimalSnifferExtension> {
+    sourceSets = listOf(main)
+    annotation = "org.mockito.internal.SuppressSignatureCheck"
+
+    // See please https://github.com/mojohaus/animal-sniffer/issues/172
+    ignore(
+        "java.lang.instrument.Instrumentation",
+        "java.lang.invoke.MethodHandle",
+        "java.lang.invoke.MethodHandles\$Lookup",
+        "java.lang.StackWalker",
+        "java.lang.StackWalker\$StackFrame",
+        "java.lang.StackWalker\$Option",
+    )
+}
+
+dependencies {
+    // Equivalent to "net.sf.androidscents.signature:android-api-level-26:8.0.0_r2@signature"
+    signature(variantOf(libs.animalSniffer.android.apiLevel26) { artifactType("signature") })
+    signature(variantOf(libs.animalSniffer.java) { artifactType("signature") })
+}
+
diff --git a/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts b/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts
new file mode 100644
index 000000000..298ce083c
--- /dev/null
+++ b/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts
@@ -0,0 +1,3 @@
+plugins {
+    base
+}
diff --git a/gradle/libs.versions.toml b/gradle/libs.versions.toml
index 895c18079..13a791aba 100644
--- a/gradle/libs.versions.toml
+++ b/gradle/libs.versions.toml
@@ -5,6 +5,8 @@ junit-jupiter = "5.11.2"
 junit-platform = "1.11.2"
 kotlin = "2.0.21"
 
+animal-sniffer = "1.7.1"
+
 [libraries]
 android-junit = { module = "androidx.test.ext:junit", version = "1.2.1" }
 android-runner = { module = "androidx.test:runner", version = "1.6.2" }
@@ -30,3 +32,10 @@ kotlin-coroutines = { module = "org.jetbrains.kotlinx:kotlinx-coroutines-core",
 objenesis = { module = "org.objenesis:objenesis", version = "3.3" }
 opentest4j = { module = "org.opentest4j:opentest4j", version = "1.3.0" }
 osgi = { module = "org.osgi:osgi.core", version = "8.0.0" }
+
+# For buildSrc logic
+gradleplugin-animalSniffer = { module = "ru.vyarus:gradle-animalsniffer-plugin", version.ref = "animal-sniffer" }
+
+# animal sniffer compatibility signatures
+animalSniffer-android-apiLevel26 = { module = "net.sf.androidscents.signature:android-api-level-26", version = "8.0.0_r2" }
+animalSniffer-java = { module = "org.codehaus.mojo.signature:java18", version = "1.0" }
diff --git a/mockito-core/build.gradle b/mockito-core/build.gradle
index 5b58fa202..a7e878755 100644
--- a/mockito-core/build.gradle
+++ b/mockito-core/build.gradle
@@ -1,8 +1,6 @@
-
 plugins {
-    id 'base'
+    id("mockito.java-backward-compatibility-checks-conventions")
     id 'biz.aQute.bnd.builder' version '7.0.0'
-    id 'ru.vyarus.animalsniffer' version '1.7.1'
 }
 
 description = 'Mockito mock objects library core API and implementation'
@@ -34,21 +32,4 @@ dependencies {
     testImplementation libs.junit.jupiter.params
 
     testUtil sourceSets.test.output
-
-    signature 'org.codehaus.mojo.signature:java18:1.0@signature'
-    signature 'net.sf.androidscents.signature:android-api-level-26:8.0.0_r2@signature'
-}
-
-animalsniffer {
-    sourceSets = [sourceSets.main]
-    annotation = 'org.mockito.internal.SuppressSignatureCheck'
-    // See please https://github.com/mojohaus/animal-sniffer/issues/172
-    ignore += [
-        'java.lang.instrument.Instrumentation',
-        'java.lang.invoke.MethodHandle',
-        'java.lang.invoke.MethodHandles$Lookup',
-        'java.lang.StackWalker',
-        'java.lang.StackWalker$StackFrame',
-        'java.lang.StackWalker$Option'
-    ]
 }
