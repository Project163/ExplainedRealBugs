diff --git a/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts b/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts
index 3758e3866..5a302a996 100644
--- a/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts
+++ b/buildSrc/src/main/kotlin/mockito.library-conventions.gradle.kts
@@ -2,4 +2,5 @@ plugins {
     base
     id("mockito.java-library-conventions")
     id("mockito.osgi-conventions")
+    id("mockito.publication-conventions")
 }
diff --git a/buildSrc/src/main/kotlin/mockito.publication-conventions.gradle.kts b/buildSrc/src/main/kotlin/mockito.publication-conventions.gradle.kts
new file mode 100644
index 000000000..86ea852f5
--- /dev/null
+++ b/buildSrc/src/main/kotlin/mockito.publication-conventions.gradle.kts
@@ -0,0 +1,165 @@
+import gradle.kotlin.dsl.accessors._2a0859b2b1624a8c1f98b2dbe4a3b98d.javadoc
+import org.gradle.api.plugins.JavaPlugin
+import org.gradle.kotlin.dsl.`maven-publish`
+import org.gradle.kotlin.dsl.withType
+
+plugins {
+    `maven-publish`
+    signing
+}
+
+// enforce the same group for all published artifacts
+group = "org.mockito"
+
+// generic publication conventions
+publishing {
+    publications {
+        withType(MavenPublication::class).configureEach {
+            pom {
+                artifactId = project.base.archivesName.get()
+                name = artifactId
+                afterEvaluate {
+                    description = project.description
+                }
+                url = "https://github.com/mockito/mockito"
+                licenses {
+                    license {
+                        name = "MIT"
+                        url = "https://opensource.org/licenses/MIT"
+                        distribution = "repo"
+                    }
+                }
+                developers {
+                    listOf(
+                        "mockitoguy:Szczepan Faber",
+                        "bric3:Brice Dutheil",
+                        "raphw:Rafael Winterhalter",
+                        "TimvdLippe:Tim van der Lippe"
+                    ).forEach { devData ->
+                        developer {
+                            val devInfo = devData.split(":")
+                            id = devInfo[0]
+                            name = devInfo[1]
+                            url = "https://github.com/" + devInfo[0]
+                            roles = listOf("Core developer")
+                        }
+                    }
+                }
+                scm {
+                    url = "https://github.com/mockito/mockito.git"
+                }
+                issueManagement {
+                    url = "https://github.com/mockito/mockito/issues"
+                    system = "GitHub issues"
+                }
+                ciManagement {
+                    url = "https://github.com/mockito/mockito/actions"
+                    system = "GH Actions"
+                }
+            }
+        }
+    }
+
+    //useful for testing - running "publish" will create artifacts/pom in a local dir
+    repositories {
+        maven(uri(rootProject.layout.buildDirectory.dir("repo"))) {
+            name = "local"
+        }
+    }
+}
+
+// Java platform publication conventions (BOM file)
+plugins.withType(JavaPlatformPlugin::class) {
+    publishing {
+        publications {
+            register<MavenPublication>("mockitoPlatform") {
+                from(components["javaPlatform"])
+            }
+        }
+    }
+}
+
+// Java library publication conventions
+plugins.withType<JavaPlugin>().configureEach {
+    // Sources/javadoc artifacts required by Maven module publications
+    val licenseSpec = copySpec {
+        from(project.rootDir)
+        include("LICENSE")
+    }
+
+    val sourcesJar by tasks.registering(Jar::class) {
+        archiveClassifier.set("sources")
+        val sourceSets: SourceSetContainer by project
+        from(sourceSets["main"].allSource)
+        with(licenseSpec)
+    }
+
+    val javadocJar by tasks.registering(Jar::class) {
+        onlyIf { tasks.javadoc.get().isEnabled }
+        archiveClassifier.set("javadoc")
+        from(tasks.named("javadoc"))
+        with(licenseSpec)
+    }
+
+    project.artifacts {
+        archives(sourcesJar)
+        if (javadocJar.get().isEnabled) {
+            archives(javadocJar)
+        }
+    }
+
+    tasks.named("jar", Jar::class) {
+        with(licenseSpec)
+    }
+
+    // only create a 'java' publication when the JavaPlugin is present
+    publishing {
+        publications {
+            register<MavenPublication>("mockitoLibrary") {
+                from(components["java"])
+                artifact(sourcesJar)
+                if (javadocJar.get().didWork) {
+                    artifact(javadocJar)
+                }
+
+                pom {
+                    // Gradle does not write 'jar' packaging to the pom (unlike other packaging types).
+                    // This is OK because 'jar' is implicit/default:
+                    // https://maven.apache.org/guides/introduction/introduction-to-the-pom.html#minimal-pom
+                    packaging = project.tasks.named<Jar>("jar").get().archiveExtension.get()
+                }
+            }
+        }
+    }
+
+    tasks.named<GenerateMavenPom>("generatePomFileForMockitoLibraryPublication") {
+        doLast {
+            // validates that the pom has correct artifact id to avoid issues like #1444
+            destination.readText().let { pomContent ->
+                require(pomContent.contains("<artifactId>${project.base.archivesName.get()}</artifactId>")) {
+                    "POM file does not contain the correct artifactId: ${project.base.archivesName.get()}"
+                }
+                require(pomContent.contains("<name>${project.base.archivesName.get()}</name>")) {
+                    "POM file does not contain the correct name: ${project.base.archivesName.get()}"
+                }
+            }
+        }
+    }
+
+    // fleshes out problems with Maven pom generation when building
+    tasks.build {
+        dependsOn("publishAllPublicationsToLocalRepository")
+    }
+}
+
+tasks.withType<GenerateModuleMetadata>().configureEach {
+    enabled = false
+}
+
+// https://docs.gradle.org/current/userguide/signing_plugin.html
+signing {
+    System.getenv("PGP_KEY")?.takeIf { it.isNotBlank() }?.let { pgpKey ->
+        useInMemoryPgpKeys(pgpKey, System.getenv("PGP_PWD"))
+        sign(publishing.publications)
+    }
+}
diff --git a/gradle/java-library.gradle b/gradle/java-library.gradle
deleted file mode 100644
index 071c7a476..000000000
--- a/gradle/java-library.gradle
+++ /dev/null
@@ -1,11 +0,0 @@
-apply plugin: "java"
-apply plugin: "java-library"
-
-apply from: "$rootDir/gradle/java-publication.gradle"
-
-generatePomFileForJavaLibraryPublication.doLast {
-    //validates the the pom has correct artifact id to avoid issues like #1444
-    def pom = new groovy.xml.XmlSlurper().parse(destination)
-    assert pom.artifactId == base.archivesName.get()
-    assert pom.name == base.archivesName.get()
-}
diff --git a/gradle/java-publication.gradle b/gradle/java-publication.gradle
deleted file mode 100644
index 7072efe3b..000000000
--- a/gradle/java-publication.gradle
+++ /dev/null
@@ -1,118 +0,0 @@
-group = 'org.mockito'
-
-plugins.withId("java") {
-    //Sources/javadoc artifacts required by Maven module publications
-    def licenseSpec = copySpec {
-        from project.rootDir
-        include "LICENSE"
-    }
-
-   def sourcesJarTask = tasks.register("sourcesJar", Jar) {
-        archiveClassifier = 'sources'
-        from sourceSets.main.allSource
-        with licenseSpec
-    }
-
-    def javadocJarTask = tasks.register("javadocJar", Jar) {
-        archiveClassifier = 'javadoc'
-        from tasks.javadoc
-        with licenseSpec
-    }
-
-    artifacts {
-        archives sourcesJarTask
-        archives javadocJarTask
-    }
-
-    tasks.named("jar", Jar) {
-        with licenseSpec
-    }
-}
-
-
-tasks.withType(GenerateModuleMetadata).configureEach {
-    enabled = false
-}
-
-//Gradle Maven publishing plugin configuration (https://docs.gradle.org/current/userguide/publishing_maven.html)
-apply plugin: "maven-publish"
-publishing {
-    publications {
-        javaLibrary(MavenPublication) {
-            plugins.withId("java") {
-                from components.java
-                artifact tasks.named("sourcesJar")
-                artifact tasks.named("javadocJar")
-            }
-            plugins.withId("java-platform") {
-                from components.javaPlatform
-            }
-
-            artifactId = project.base.archivesName.get()
-
-            pom {
-                name = artifactId
-                afterEvaluate {
-                    description = project.description
-                }
-
-                plugins.withId("java") {
-                    //Gradle does not write 'jar' packaging to the pom (unlike other packaging types).
-                    //This is OK because 'jar' is implicit/default:
-                    // https://maven.apache.org/guides/introduction/introduction-to-the-pom.html#minimal-pom
-                    packaging = project.tasks.jar.archiveExtension.get()
-                }
-
-                url = "https://github.com/mockito/mockito"
-                licenses {
-                    license {
-                        name = 'MIT'
-                        url = 'https://opensource.org/licenses/MIT'
-                        distribution = 'repo'
-                    }
-                }
-                developers {
-                    ['mockitoguy:Szczepan Faber', 'bric3:Brice Dutheil', 'raphw:Rafael Winterhalter',
-                     'TimvdLippe:Tim van der Lippe'].each { devData ->
-                        developer {
-                            def devInfo = devData.split(':')
-                            id = devInfo[0]
-                            name = devInfo[1]
-                            url = 'https://github.com/' + devInfo[0]
-                            roles = ["Core developer"]
-                        }
-                    }
-                }
-                scm {
-                    url = 'https://github.com/mockito/mockito.git'
-                }
-                issueManagement {
-                    url = 'https://github.com/mockito/mockito/issues'
-                    system = 'GitHub issues'
-                }
-                ciManagement {
-                    url = 'https://github.com/mockito/mockito/actions'
-                    system = 'GH Actions'
-                }
-            }
-        }
-    }
-
-    //useful for testing - running "publish" will create artifacts/pom in a local dir
-    repositories { maven { url = layout.buildDirectory.dir("repo") } }
-}
-
-plugins.withId("java") {
-    //fleshes out problems with Maven pom generation when building
-    tasks.named("build") {
-        dependsOn("publishJavaLibraryPublicationToMavenLocal")
-    }
-}
-
-apply plugin: 'signing' //https://docs.gradle.org/current/userguide/signing_plugin.html
-signing {
-    if (System.getenv('PGP_KEY')) {
-        useInMemoryPgpKeys(System.getenv('PGP_KEY'), System.getenv('PGP_PWD'))
-        sign publishing.publications.javaLibrary
-    }
-}
diff --git a/mockito-bom/build.gradle b/mockito-bom/build.gradle
index 4ebba66e3..81d86d3dc 100644
--- a/mockito-bom/build.gradle
+++ b/mockito-bom/build.gradle
@@ -1,5 +1,6 @@
 plugins {
-    id 'java-platform'
+    id("java-platform")
+    id("mockito.publication-conventions")
 }
 
 description = "Mockito Bill of Materials (BOM)"
@@ -9,8 +10,6 @@ if (!base.archivesName.get().startsWith("mockito-")) {
     base.archivesName = "mockito-" + project.name
 }
 
-apply from: "$rootDir/gradle/java-publication.gradle"
-
 dependencies {
     constraints {
         api(project(":mockito-core"))
diff --git a/mockito-core/build.gradle b/mockito-core/build.gradle
index bd31c6f90..a1e606736 100644
--- a/mockito-core/build.gradle
+++ b/mockito-core/build.gradle
@@ -6,8 +6,6 @@ plugins {
 description = 'Mockito mock objects library core API and implementation'
 base.archivesName = 'mockito-core'
 
-
-apply from: "$rootDir/gradle/java-library.gradle"
 apply from: "$rootDir/gradle/coverage.gradle"
 apply from: "$rootDir/gradle/mockito-core/inline-mock.gradle"
 apply from: "$rootDir/gradle/mockito-core/javadoc.gradle"
diff --git a/mockito-extensions/mockito-android/build.gradle b/mockito-extensions/mockito-android/build.gradle
index e39e69ae4..2b8bb1f90 100644
--- a/mockito-extensions/mockito-android/build.gradle
+++ b/mockito-extensions/mockito-android/build.gradle
@@ -1,9 +1,8 @@
 plugins {
     id("mockito.java-library-conventions")
+    id("mockito.publication-conventions")
 }
 
-apply from: "$rootDir/gradle/java-library.gradle"
-
 description = "Mockito for Android"
 
 dependencies {
diff --git a/mockito-extensions/mockito-errorprone/build.gradle b/mockito-extensions/mockito-errorprone/build.gradle
index 282075fa2..4ea12895a 100644
--- a/mockito-extensions/mockito-errorprone/build.gradle
+++ b/mockito-extensions/mockito-errorprone/build.gradle
@@ -1,9 +1,7 @@
 plugins {
-    id("mockito.java-library-conventions")
+    id("mockito.library-conventions")
 }
 
-apply from: "$rootDir/gradle/java-library.gradle"
-
 description = "ErrorProne plugins for Mockito"
 
 dependencies {
diff --git a/mockito-extensions/mockito-junit-jupiter/build.gradle b/mockito-extensions/mockito-junit-jupiter/build.gradle
index 05508321d..4684632b7 100644
--- a/mockito-extensions/mockito-junit-jupiter/build.gradle
+++ b/mockito-extensions/mockito-junit-jupiter/build.gradle
@@ -4,8 +4,6 @@ plugins {
     id("mockito.library-conventions")
 }
 
-apply from: "$rootDir/gradle/java-library.gradle"
-
 apply from: "$rootDir/gradle/mockito-junit-jupiter/javadoc.gradle"
 
 description = "Mockito JUnit 5 support"
diff --git a/mockito-extensions/mockito-proxy/build.gradle b/mockito-extensions/mockito-proxy/build.gradle
index 3ef9d2b54..ecd645efa 100644
--- a/mockito-extensions/mockito-proxy/build.gradle
+++ b/mockito-extensions/mockito-proxy/build.gradle
@@ -1,9 +1,7 @@
 plugins {
-    id("mockito.java-library-conventions")
+    id("mockito.library-conventions")
 }
 
-apply from: "$rootDir/gradle/java-library.gradle"
-
 description = "Mockito preconfigured proxy mock mock maker (to support interfaces without code generation)"
 
 dependencies {
diff --git a/mockito-extensions/mockito-subclass/build.gradle b/mockito-extensions/mockito-subclass/build.gradle
index b8a5e6f43..ac116e06b 100644
--- a/mockito-extensions/mockito-subclass/build.gradle
+++ b/mockito-extensions/mockito-subclass/build.gradle
@@ -1,7 +1,6 @@
 plugins {
-    id("mockito.java-library-conventions")
+    id("mockito.library-conventions")
 }
-apply from: "$rootDir/gradle/java-library.gradle"
 
 description = "Mockito preconfigured subclass mock maker"
 
